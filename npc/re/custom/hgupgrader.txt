//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Update HG-Upgrader By: ==================================================
//= Boy
//===== Current Version: =====================================
//= 1.4a
//===== Compatible With: =====================================
//= rAthena SVN r15340+
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//============================================================
					
prontera,141,227,5	script	HG-Upgrader	763,{
function Add; function Chk; function Slot; function A_An;
if (checkweight(21016,1) == 0) {
	mes "[^FF6600Reno^000000]";
    mes "You're carrying too much stuffs , make some space and come back to me later";
        close;	
	}
	mes "[^FF6600Reno^000000]";
	mes "Hello, I am Reno.";
	mes "I can upgrade famous gears for you.";
	mes "Do you want to see the list ?";
	next;
	switch( select( "Yes.:No." ) ) {
case 1: 		
	mes "[^FF6600Reno^000000]";
	mes "Here's the list.";
	break; 
case 2: 	
	mes "[^FF6600Reno^000000]";
    mes "Okay, see you again.";
   	close;
}
	if(.Shops$ != "") set .@i,1;
	else {
		set .@menu$,"";
		for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1)
			set .@menu$, .@menu$+.Shops$[.@i]+":";
		set .@i, select(.@menu$); }
		mes "^0055FFYou can only select one item at a time.^000000";
	callshop "hgupgrader"+.@i,1;
	npcshopattach "hgupgrader"+.@i;
	end;

function Add {
	if (getitemname(getarg(1))=="null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped)."; return; }
	for(set .@n,5; .@n<127; set .@n,.@n+2) {
		if (!getarg(.@n,0)) break;
		if (getitemname(getarg(.@n))=="null") {
			debugmes "Quest requirement #"+getarg(.@n)+" invalid (skipped) on Quest ItemID "+ getarg(1) +"."; return; } }
	for(set .@i,2; .@i<.@n; set .@i,.@i+1)
		set getd(".q_"+getarg(1)+"["+(.@i-2)+"]"), getarg(.@i);
	npcshopadditem "hgupgrader"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return; }
function Chk {
	if (getarg(0)<getarg(1)) { set @qe0,1; return "^FF0000"; }
	else return "^00FF00"; }
function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot){
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),11)>0) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$; } }
function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0); }

OnBuyItem:
	set .@q[0],@bought_nameid;
	copyarray .@q[1],getd(".q_"+@bought_nameid+"[0]"),getarraysize(getd(".q_"+@bought_nameid+"[0]"));
	if (!.@q[1]) { message strcharinfo(0),"An error has occurred."; end; }
	mes "[^FF6600Reno^000000]";
	mes "Your Choice: ^0055FF"+((.@q[1]>1)?.@q[1]+"x ":"")+Slot(.@q[0])+"^000000";
	mes	"Estimated success chance: ^FF660020%^000000 ";
	mes "Requirements:";
	if (.@q[2]) mes " > "+Chk(Zeny,.@q[2])+.@q[2]+" Zeny^000000";
	if (.@q[3]) mes " > "+Chk(getd(.Points$[0]),.@q[3])+.@q[3]+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+.@q[3]+")^000000";
	if (.@q[4]) for(set .@i,4; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1])+((.DisplayID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+.@q[.@i+1]+")^000000";
	next;
	set @qe1, getiteminfo(.@q[0],5); set @qe2, getiteminfo(.@q[0],11);
	addtimer 1000, strnpcinfo(1)+"::OnEnd";
	while(1){
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((((@qe1&1) || (@qe1&256) || (@qe1&512) || (@qe1&1024) || (@qe1&2048) || (@qe1&4096)) && @qe2>0 && !@qe6)?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
			case 1:
				if (@qe0) { 
					mes "[^FF6600Reno^000000]";
					mes "You're missing one or more quest requirements.";
					close; }
				if (!checkweight(.@q[0],.@q[1])) {
					mes "[^FF6600Reno^000000]";
					mes "^FF0000You need "+(((.@q[1]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
					close; }
				if (.@q[2]) set Zeny, Zeny-.@q[2];
				if (.@q[3]) setd .Points$[0], getd(.Points$[0])-.@q[3];
				if (.@q[4]) for(set .@i,4; .@i<getarraysize(.@q); set .@i,.@i+2)
					delitem .@q[.@i],.@q[.@i+1];
			if (rand(1,5) == 1) {
				if (.Announce) announce strcharinfo(0)+" has created "+A_An(getitemname(.@q[0]))+"!",0;
				getitem .@q[0],.@q[1];
				specialeffect2 699;
					mes "[^FF6600Reno^000000]";
					mes "Woah, you did it.";
					mes "Congratulation !.";
					close;				
			} 
			else {
					specialeffect EF_SUI_EXPLOSION;
					mes "[^FF6600Reno^000000]";
					mes "Woah, exploded like a firecracker.";
					mes "hahahahahaha.";
					close; 
			}			
			case 2:
				set @qe3, getlook(3); set @qe4, getlook(4); set @qe5, getlook(5);
				if (@qe1&1) atcommand "@changelook 3 "+@qe2;
				if (@qe1&256) atcommand "@changelook 1 "+@qe2;
				if (@qe1&512) atcommand "@changelook 2 "+@qe2;
				if (@qe1&1024) atcommand "@changelook 1 "+@qe2;
				if (@qe1&2048) atcommand "@changelook 1 "+@qe2;
				if (@qe1&4096) atcommand "@changelook 1 "+@qe2;
				set @qe6,1;
				break; 
			case 3:
				close; } }
OnEnd:
	if (@qe6) { atcommand "@changelook 3 "+@qe3; atcommand "@changelook 1 "+@qe4; atcommand "@changelook 2 "+@qe5; }
	for(set .@i,0; .@i<7; set .@i,.@i+1) setd "@qe"+.@i,0;
	end;
OnInit:
// --------------------- Config ---------------------
// Custom points, if needed: "<variable>","<name to display>"
	setarray .Points$[0],"#CASHPOINTS","Cash Points";

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,2;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .DisplayID,1;	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,1;	// Show Zeny cost, if any? (1: yes / 0: no)

// Shop categories, if needed: "<Shop 1>","<Shop 2>"{,...};
// Duplicate dummy data for any additional shops (bottom of script).
// If no categories, use the second line instead (remove //).

	setarray .Shops$[1],"2012 Headgear";
	setarray .Shops$[2],"Kafra Headgear";
//	setarray .Shops$[3],"New Headgear";
	// set .Shops$,"n/a";

// Add(<shop number>,<reward ID>,<reward amount>,<Zeny cost>,<point cost>,
//     <required item ID>,<required item amount>{,...});
// Shop number corresponds with order above (default is 1).
// Note: Do NOT use a reward item more than once!

	Add(1,18760,1,100000,0,5027,1,7619,4);
	Add(1,18761,1,100000,0,5045,1,7619,4);
	Add(1,18762,1,100000,0,5069,1,7619,4);
	Add(1,18763,1,100000,0,5003,1,7619,4);
	Add(1,18764,1,100000,0,2214,1,7619,4);
	Add(1,18769,1,100000,0,5167,1,7619,4);
	Add(1,18770,1,100000,0,5168,1,7619,4);
	Add(1,18771,1,100000,0,5043,1,7619,4);
	sleep 10;
	Add(1,18772,1,100000,0,2296,1,7619,6);
	Add(1,18773,1,100000,0,5014,1,7619,6);
	Add(1,18774,1,100000,0,5096,1,7619,6);
	Add(1,18775,1,100000,0,2292,1,7619,6);
	Add(1,2956,1,100000,0,2615,1,2621,1,2624,1,2622,1,2625,1,2623,1,2626,1,7619,6);
	Add(1,15068,1,100000,0,2355,1,7619,8);
	Add(1,2183,1,100000,0,2116,1,7619,8);
	Add(1,22015,1,100000,0,2420,1,7619,8);
	Add(1,18776,1,100000,0,5100,1,7619,8);
	Add(1,20710,1,100000,0,2521,1,7619,8);
	sleep 10;
	Add(1,18765,1,100000,0,5019,1,7619,10);
	Add(1,18768,1,100000,0,5162,1,7619,10);
	Add(1,18766,1,100000,0,5025,1,7619,10);
	Add(1,22014,1,100000,0,2423,1,7619,10);
	Add(1,2957,1,100000,0,2678,1,7619,10);
	Add(1,2958,1,100000,0,2679,1,7619,10);
	Add(1,18767,1,1000000,0,5022,1,7619,20);
	
	Add(2,5387,1,100000,0,5020,1,7619,8);
	Add(2,5253,1,100000,0,5141,1,979,2,7619,8);
	Add(2,5340,1,100000,0,5020,1,978,2,7619,8);
	Add(2,5341,1,100000,0,5020,1,982,2,7619,8);
	Add(2,5342,1,100000,0,5020,1,983,2,7619,8);
	Add(2,5343,1,100000,0,5020,1,981,2,7619,8);
	Add(2,5344,1,100000,0,5020,1,980,2,7619,8);
	Add(2,5345,1,100000,0,5020,1,976,2,7619,8);
	Add(2,5607,1,100000,0,5020,1,975,2,7619,8);
	Add(2,5608,1,100000,0,5020,1,978,1,975,1,7619,8);
	
	Add(3,5360,1,500000,0,5057,1,7619,10,6417,5); // Wickebine
	Add(3,5593,1,800000,0,5109,1,6020,50,7619,10,6417,8); // Rabbit Bonnet
	Add(3,5520,1,800000,0,2283,1,7619,10,6417,8); // Rabbit Earmuffs		
	Add(3,5226,1,1000000,0,2259,1,7619,10,6417,10); // Propeller
	Add(3,18539,1,1000000,0,5016,1,7005,50,7619,10,6417,10); // Skull Cap
	Add(3,18508,1,1000000,0,5071,1,7440,10,7619,10,6417,10); // Garuda Hat	
//	Add(3,18950,1,1000000,0,5038,1,7619,10,6417,10); // Deviruchi
	Add(3,5579,1,1000000,0,7005,10,1059,10,2280,1,983,1,7619,10,6417,10); // Sakkat

// --------------------------------------------------
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1)
		npcshopdelitem "hgupgrader"+.@i,909;
	end;
}

// -------- Dummy data (duplicate as needed) --------
-	shop	hgupgrader1	-1,909:-1
-	shop	hgupgrader2	-1,909:-1
//-	shop	hgupgrader3	-1,909:-1